# >======| The Brain Of the Cat |=====< #


# hunger
import is_hungery?: "res://assets/scripts/cat/bt_actions/is_hungery.gd"
import can_eat?: "res://assets/scripts/cat/bt_actions/can_eat.gd"
import find_food_point: "res://assets/scripts/cat/bt_actions/find_food_point.gd"
import eat: "res://assets/scripts/cat/bt_actions/eat.gd"

# thirsty
import is_thirsty?: "res://assets/scripts/cat/bt_actions/is_thirsty.gd"
import can_drink?: "res://assets/scripts/cat/bt_actions/can_drink.gd"
import find_water_point: "res://assets/scripts/cat/bt_actions/find_water_point.gd"
import drink: "res://assets/scripts/cat/bt_actions/drink.gd"

# common
import find_random_point: "res://assets/scripts/cat/bt_actions/find_random_point.gd"
import go_to_target: "res://assets/scripts/cat/bt_actions/go_to_target.gd"
import miao: "res://assets/scripts/cat/bt_actions/miao.gd"
import miao_msg: "res://assets/scripts/cat/bt_actions/miao_msg.gd"

subtree name: go_to_somewhere
	sequence
		find_random_point
		go_to_target

subtree name: wander
	sequence
		$go_to_somewhere
		random_timer min_wait: 0.5 max_wait: 1.5

subtree name: find_food_to_eat
	selector
		sequence
			miao_msg s: "吃饭咯~"
			find_food_point
			go_to_target
			can_eat?
			always_succeed
				until_fail
					sequence
						eat
						timer wait: 0.5
						can_eat?
			miao_msg hide: true
		
		parallel policy: SEQUENCE orchestrator: RESUME
			miao s: "嘿，没有食物啦！"
			$wander
			invert
				sequence
					find_food_point set_target: false
					timer wait: 0.5

subtree name: find_water_to_drink
	selector
		sequence
			miao_msg s: "喝水去~"
			find_water_point
			go_to_target
			can_drink?
			always_succeed
				until_fail
					sequence
						drink
						timer wait: 0.5
						can_drink?
			miao_msg hide: true
		
		parallel
			miao s: "嘿，没有水啦！"
			$wander
			invert
				sequence
					find_water_point set_target: false
					timer wait: 0.5

tree
	selector
		(is_thirsty?) $find_water_to_drink
		(is_hungery?) $find_food_to_eat
		parallel
			$wander
			invert
				selector
					is_hungery?
					is_thirsty?


