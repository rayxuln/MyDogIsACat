# >======| The Brain Of the Cat |=====< #

# desire
import is_desiring?: "res://assets/scripts/cat/bt_actions/is_desiring.gd"
import can_fullfill?: "res://assets/scripts/cat/bt_actions/can_fullfill.gd"
import find_desire_feeder: "res://assets/scripts/cat/bt_actions/find_desire_feeder.gd"
import interact: "res://assets/scripts/cat/bt_actions/interact.gd"

# common
import find_random_point: "res://assets/scripts/cat/bt_actions/find_random_point.gd"
import go_to_target: "res://assets/scripts/cat/bt_actions/go_to_target.gd"
import miao: "res://assets/scripts/cat/bt_actions/miao.gd"
import miao_msg: "res://assets/scripts/cat/bt_actions/miao_msg.gd"

subtree name: go_to_somewhere
	sequence
		find_random_point
		go_to_target

subtree name: wander
	sequence
		$go_to_somewhere
		random_timer min_wait: 0.5 max_wait: 1.5

subtree name: find_feeder_to_interact
	selector
		sequence
			miao_msg type: "GoToFeeder"
			find_desire_feeder
			go_to_target
			can_fullfill?
			always_succeed
				until_fail
					sequence
						interact
						timer wait: 0.5
						can_fullfill?
			miao_msg hide: true
		
		parallel policy: SEQUENCE orchestrator: RESUME
			miao type: "FeederIsEmpty"
			$wander
			invert
				sequence
					find_desire_feeder set_target: false
					timer wait: 0.5

tree
	selector
		(is_desiring?) $find_feeder_to_interact
		parallel
			$wander
			invert
				is_desiring?


